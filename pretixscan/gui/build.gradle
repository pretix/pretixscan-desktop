/*
 * Open Source Software published under the Apache Licence, Version 2.0.
 */
import org.apache.tools.ant.filters.*

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}
apply from: '../libpretixsync-repo/libpretixsync/versions.gradle'

mainClassName = "eu.pretix.pretixscan.desktop.PretixScanMain"

applicationName = "pretixSCAN"

ext.buildTimestamp = new Date().format('yyyy-MM-dd HH:mm:ss')

processResources {
    filesMatching('properties/*.properties') {
        filter ReplaceTokens, tokens: [
                'build.version'  : project.property("version"),
                'build.timestamp': project.buildTimestamp
        ]
    }
}

jar {
    from(buildDir) {
        include 'build-info.properties'
    }
}

def platform
if (operatingSystem.isWindows()) {
    platform = 'win'
} else if (operatingSystem.isLinux()) {
    platform = 'linux'
} else if (operatingSystem.isMacOsX()) {
    platform = 'mac'
}

configurations {
    javafxCompile
}

javafx {
    modules = [
            'javafx.controls', 'javafx.fxml', 'javafx.swing'
    ]
    version = '11.0.2'
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    implementation fileTree(dir: '../tornadofx-repo/target', include: ['*.jar'])
    implementation "org.xerial:sqlite-jdbc:$sqlite_jdbc_version"
    implementation 'net.harawata:appdirs:1.2.1'
    implementation 'net.java.dev.jna:platform:3.5.2'
    implementation "joda-time:joda-time:$joda_version"
    implementation "de.huxhorn.lilith:de.huxhorn.lilith.3rdparty.junique:1.0.4"
    implementation 'com.github.kittinunf.fuel:fuel:2.1.0'
    implementation 'de.jensd:fontawesomefx-materialicons:2.2.0-9.1.2'
    implementation 'com.jfoenix:jfoenix:9.0.9'
    implementation 'org.apache.pdfbox:pdfbox:2.0.13'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.66'
    implementation 'com.github.sarxos:webcam-capture:0.3.12'

    implementation(project(':libpretixsync')) {
        transitive = false
    }
    implementation(project(':libpretixprint')) {
        transitive = false
    }
    testImplementation group: 'junit', name: 'junit', version: '4.12'

    implementation "org.openjfx:javafx-base:11.0.2:${platform}"
    implementation "org.openjfx:javafx-graphics:11.0.2:${platform}"
    implementation "org.openjfx:javafx-controls:11.0.2:${platform}"
    implementation "org.openjfx:javafx-fxml:11.0.2:${platform}"
    implementation "org.openjfx:javafx-swing:11.0.2:${platform}"

    //compileOnly configurations.javafxCompile
}

tasks.withType(JavaCompile) {
    options.with {
        compilerArgs += [
                '--module-path', configurations.javafxCompile.asPath,
                '--add-modules', 'javafx.controls'
        ]
    }
}

[run, test].each {
    it.jvmArgs += [
            '--module-path', configurations.javafxCompile.asPath,
            '--add-modules', 'javafx.controls,javafx.fxml,javafx.graphics,javafx.swing',
            '--add-exports', 'javafx.base/com.sun.javafx.runtime=ALL-UNNAMED',
            '--add-exports', 'javafx.graphics/com.sun.javafx.css=ALL-UNNAMED',
            '--add-exports', 'javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED',
            '--add-exports', 'javafx.controls/com.sun.javafx.scene.control.inputmap=ALL-UNNAMED',
    ]
}


javafx {
    modules = [
            'javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.swing'
    ]
    configurations = [ 'implementation', 'testImplementation' ]
    version = '11.0.2'
}