group 'eu.pretix.pretixdesk'
version = '0.3.3'
version version

buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'edu.sc.seis.gradle:launch4j:2.4.2'
        classpath group: 'de.dynamicfiles.projects.gradle.plugins', name: 'javafx-gradle-plugin', version: '8.8.2'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'javafx-gradle-plugin'
apply plugin: 'edu.sc.seis.launch4j'

sourceCompatibility = 1.9
mainClassName = 'eu.pretix.pretixdesk.PretixDeskMain'

repositories {
    mavenCentral()
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.1"
        languageVersion = "1.1"
    }
}
dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'no.tornado:tornadofx:1.7.14'
    compile 'org.xerial:sqlite-jdbc:3.20.1'
    compile 'net.harawata:appdirs:1.0.0'
    compile 'net.java.dev.jna:platform:3.5.2'
    compile 'joda-time:joda-time:2.9.9'
    compile "de.huxhorn.lilith:de.huxhorn.lilith.3rdparty.junique:1.0.4"
    compile 'com.github.kittinunf.fuel:fuel:1.12.0'
    compile 'de.jensd:fontawesomefx-materialicons:2.2.0-9.1.2'
    compile(project(':libpretixsync'))
    compile files('libs/jfoenix-9.0.0.jar')
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jfx {
    mainClass = mainClassName
    vendor = 'pretix.eu'
    jfxMainAppJarName = "pretixdesk-${version}.jar"

    // gradle jfxNative
    identifier = null
    nativeReleaseVersion = version
    needShortcut = false
    needMenu = false
    appName = "pretixdesk"
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    archiveName = project.name + '.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

launch4j {
    mainClassName = mainClassName
    jar = "lib/${project.tasks.fatJar.archiveName}"
    copyConfigurable = project.tasks.fatJar.outputs.files
    bundledJrePath = "jre"
    bundledJre64Bit = true
    version = version
    fileDescription = "pretix Check-in application"
    copyright = "pretix.eu"
    companyName = "pretix.eu"
    productName = "pretixdesk"
    //splashFileName = "../../../img/splash.bmp"
    icon = "../../../img/icon_on_shape.ico"
}
